import org.gradle.api.plugins.jetty.JettyRunWar

apply plugin: 'java'
apply plugin: 'jetty'

repositories {
    mavenLocal()
    mavenCentral()
}

// custom configuration for running the webapp
configurations {
    solrWebApp
}

dependencies {
    solrWebApp "org.apache.solr:solr:4.2.0@war"
    testCompile "junit:junit:4.11"
    testCompile("org.apache.solr:solr-test-framework:4.2.0") {
        exclude group: "org.restlet.jee"
    }
}

// custom task that configures the jetty plugin
task runSolr(type: JettyRunWar) {
    webApp = configurations.solrWebApp.singleFile

    // jetty configuration
    httpPort = 8082
    contextPath = 'solr'
}

// executed before jetty plugin
runSolr.doFirst {
    System.setProperty("solr.solr.home", "./solrhome")
}
